plugins {
    id 'java'
    id 'java-library'
}

group 'com.gg'

repositories {
    mavenCentral()
}

// JAXB configuration holds classpath for running the JAXB XJC compiler
configurations {
    jaxb
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
        resources {
            srcDir 'src/resources'
        }
    }
    generated {
        java.srcDir "src/main/java"
    }
}
dependencies {
    jaxb "org.glassfish.jaxb:jaxb-xjc:4.0.1"
    jaxb "org.glassfish.jaxb:jaxb-runtime:4.0.1"

    api('org.junit.jupiter:junit-jupiter-api:5.8.1')
    api('org.junit.jupiter:junit-jupiter-engine:5.8.1')
    api('org.springframework.boot:spring-boot-test:3.1.5')
    api('org.hamcrest:hamcrest:2.2')
    api('org.mockito:mockito-core:5.4.0')
    api('org.mockito:mockito-junit-jupiter:5.4.0')

    api('org.springframework:spring-core:6.0.3')
    api('org.springframework:spring-webmvc:6.0.3')
    api('org.springframework.boot:spring-boot:3.1.5')
    api('org.springframework.boot:spring-boot-autoconfigure:3.1.5')
    api('com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.14.0')
    api('javax.annotation:javax.annotation-api:1.3.2')
    api('javax.inject:javax.inject:1')

    api('org.apache.logging.log4j:log4j-api:2.20.0')
    api('org.apache.logging.log4j:log4j-core:2.20.0')
    api('org.apache.logging.log4j:log4j-slf4j2-impl:2.20.0')

    api('com.ibm.mq:mq-jms-spring-boot-starter:3.1.5') {
        exclude module: 'spring-boot-starter-logging'
    }

    api('jakarta.xml.bind:jakarta.xml.bind-api:4.0.0')
    api('com.sun.xml.bind:jaxb-impl:4.0.0')

    implementation('jakarta.xml.bind:jakarta.xml.bind-api:4.0.0')
    implementation('com.sun.xml.bind:jaxb-impl:4.0.0')

}

// Cookie cutter function for defining multiple XJC tasks
def addXjcTask(taskName, schema, pkg, dest) {

    // If you haven't already, create the generated output dir before running XJC or it will fail
    file(dest).mkdirs()


    // The main XJC task, calls XJCFacade which is the entry point of the XJC JAR
    tasks.create(name: taskName, type: JavaExec) {
        classpath configurations.jaxb
        mainClass = 'com.sun.tools.xjc.XJCFacade'

        // To explore available args, download the XJC JAR and run java -jar jaxb-xjc.jar --help
        args schema, "-p", pkg, "-d", dest, "-no-header", "-extension"
    }
}

// Add all the XJC tasks you need
addXjcTask("maven-xsd",
        "src/main/java/schema/",
        "com.gg.generated",
        "src/main/java")

tasks.register("generateSources") {
    description "Generate Java sources fram XSD files"
    dependsOn tasks.getByName("maven-xsd")
}

tasks.withType(JavaCompile).configureEach {
    dependsOn("generateSources")
}
